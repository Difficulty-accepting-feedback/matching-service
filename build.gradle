plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'jacoco'
}

group = 'com.grow'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
	toolVersion = "0.8.13"
}

def jacocoExcludes = [
		'**/exception/**',
		'**/dto/**',
		'**/entity/**',
		'**/Q*.class',
		'config/**',
		'**/MatchingServiceApplication.class',
		'**/enums/**',
		'**/rsdata/**',
		'**/controller/**'
]

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect { dir ->
					fileTree(dir: dir, excludes: jacocoExcludes)
				})
		)
	}

	violationRules {
		rule {
			enabled = true
			element = 'CLASS'
			limit {
//				counter = 'LINE'
				value = 'COVEREDRATIO'
//				minimum = 0.70
			}
		}
	}
}

jacocoTestReport {
	dependsOn test

	reports {
		html.required.set(true)
		html.outputLocation = layout.buildDirectory.dir("jacoco/html")
		xml.required.set(true)
		csv.required.set(false)
	}

	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect { dir ->
					fileTree(dir: dir, excludes: jacocoExcludes)
				})
		)
	}

	finalizedBy(jacocoTestCoverageVerification)
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.wiremock:wiremock-jetty12:3.9.1'

	// db
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j:8.3.0'

	// Querydsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// feign
	implementation platform("org.springframework.cloud:spring-cloud-dependencies:2025.0.0")
	implementation "org.springframework.cloud:spring-cloud-starter-openfeign"

	// redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.5.3'

	// Actuator + AOP 추가 메트릭스 모니터링
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	// resilience4j 추가
	implementation 'io.github.resilience4j:resilience4j-all:2.3.0'
	implementation 'io.github.resilience4j:resilience4j-spring-boot3'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
	maxHeapSize = "2048m"
	finalizedBy jacocoTestReport
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

tasks.withType(JavaCompile) {
	options.compilerArgs += ['-parameters']
}